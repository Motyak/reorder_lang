#!/usr/bin/env monlang

include <smallstd.mlp>
include <utils/parsing.mlp>

var builtin::print print
var builtin::getline getline

"override builtins"
var print print
var getline getline

var convertNegLineNb {
    var lines []
    var slurp_stdin _
    slurp_stdin := ():{
        var line builtin::getline()
        line == $nil || {
            lines += line
            _ := slurp_stdin()
        }
    }

    var i _
    var 1st_time_called? $true
    var convertNegLineNb (nb, context):{
        1st_time_called? && {
            i := context.currLineNb
            slurp_stdin()
            getline := ():{
                len(lines) > 0 || die()
                var line lines[#1]
                lines := tern(len(lines) == 1, [], lines[#2..-1])
                i += 1
                line
            }
            1st_time_called? := $false
        }
        nb <= len(lines) || die("Out of bounds: `-" + nb + "`")
        i + len(lines) - nb + 1
    }
    convertNegLineNb
}


var consumeExtra (OUT input):{
    var extras "\n" + " " + Byte(9)
    var nth 1
    until(():{nth > len(input) || input[#nth] !in extras}, (_):{
        nth += 1
    })

    input := tern(nth > len(input), "", input[#nth..-1])
}

var peekLineNb (input):{
    len(input) >= 1 && (input[#1] in "+-" || input[#1] in '0 .. '9)
}

var consumeLineNb (OUT input):{
    len(input) >= 1 || die()

    var sign ""
    input[#1] in "+-" && {
        len(input) >= 2 || die("Invalid line number in `" + input + "`")
        input[#2] in '0 .. '9 || die("Invalid line number in `" + input + "`")
        sign := input[#1]
        input := input[#2..-1]
    }

    var nth 1
    until(():{nth > len(input) || input[#nth] !in '0 .. '9}, (_):{
        nth += 1
    })
    
    var str tern(input == "", "", input[#1..<nth])
    str <> "" || die("Invalid line number in `" + input + "`")
    input := tern(nth > len(input), "", input[#nth..-1])
    var nb parseInt(str)

    ['sign:sign, 'nb:nb]
}

var interpretLineNb (_lineNb, OUT context):{
    var sign _lineNb.sign
    var nb _lineNb.nb
    let i context.currLineNb

    var lineEnd {
        var lineEnd nb
        sign == "-" && {lineEnd := convertNegLineNb(lineEnd, context)}
        context.exclusiveRange? && {lineEnd -= 1}
        sign == "+" && {lineEnd := i + lineEnd}
        lineEnd
    }

    until(():{i == lineEnd}, (_):{
        i += 1
        var line getline()

        "skip to first line number and print it"
        not(context.succeedsRange?) && i == lineEnd && {
            line == $nil || print(line)
        }

        "print all"
        context.succeedsRange? && {
            line == $nil || print(line)
        }
    })
}

var evalLineNb (OUT input, OUT context):{
    var lineNb consumeLineNb(&input)
    interpretLineNb(lineNb, &context)
}

var evalLines (OUT input, OUT context):{
    do_while((1st_it?):{
        1st_it? || {
            discard(&input, 1) -- ","
            "make sure it's not a trailing comma"
            peekLineNb(input) || peekStr(input, "..") || {
                die("Trailing comma in `," + input + "`")
            }
            consumeExtra(&input)
        }

        peekLineNb(input) && {
            evalLineNb(&input, &context)
            consumeExtra(&input)
        }

        peekStr(input, "..") && {
            discard(&input, 2)
            consumeExtra(&input)
            context.succeedsRange? := $true
            var case CaseAnalysis(Bool)

            case(peekLineNb(input), {
                var lineNb consumeLineNb(&input)
                consumeExtra(&input)
                peekStr(input, "[") && {
                    discard(&input, 1)
                    consumeExtra(&input)
                    context.exclusiveRange? := $true
                }
                interpretLineNb(lineNb, &context)
                context.exclusiveRange? := $false
            })

            case(_, {
                peekStr(input, "[") && {
                    discard(&input, 1)
                    consumeExtra(&input)
                    context.exclusiveRange? := $true
                }
                "handle open end range"
                interpretLineNb(['sign:"-", 'nb:1], &context)
                context.exclusiveRange? := $false
            })

            context.succeedsRange? := $false
        }
    }, ():{peekStr(input, ",")})
}

var evalProgram _

var evalCommand (OUT input, OUT context):{
    var peek CaseAnalysis((c):{peekStr(input, c)})

    -- {
        peek("q", evalQueueOp(&input, &context))
        peek("Q", evalUnqueueOp(&input, &context))
        peek("s", evalStackOp(&input, &context))
        peek("S", evalUnstackOp(&input, &context))
    }

    peek(_, {
        var lines? peekLineNb(input)
        lines? ||= peekStr(input, "..") || peekStr(input, "{")
        lines? || die("Unknown operation in `" + input + "`")
        evalLines(&input, &context)
    })
    ;
}

evalProgram := (OUT input, OUT context):{
    consumeExtra(&input)
    until(():{input == ""}, (1st_it?):{
        not(1st_it?) && peekStr(input, ";") && {
            discard(&input, 1)
            consumeExtra(&input)
        }
        evalCommand(&input, &context)
        consumeExtra(&input)
    })
}

var prog $args[#1]
var context [
    'currLineNb => 0
    'succeedsRange? => $false
    'exclusiveRange? => $false
]

evalProgram(&prog, &context)
